apply plugin: 'com.android.application'
//apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.tavultesoft.kmapro"
        minSdkVersion 15
        targetSdkVersion 28

        //dumpProperties(project.ext) // Use this to dump all external properties for debugging TeamCity integration

        if (project.hasProperty("services_json_file")) {
            println "Copying production google-services.json and using at kMAPro/"
            copy {
                from project.ext.services_json_file
                include '*.json'
                into './'
            }
        }

        if (project.hasProperty("build.number")) {
            versionCode project.ext['build_counter'] as Integer
            // Because TeamCity does not bubble build.counter into system properties...
            versionName "${project.ext['build.number']}"
        } else {
            String majorMinorVersion = file('$projectDir/../../../../resources/VERSION.md').text.trim()
            versionCode 100
            versionName majorMinorVersion + '.0.0'
        }
    }

    if (project.hasProperty("release_store_file")) {
        signingConfigs {
            release {
                storeFile file(project.ext.release_store_file)
                storePassword project.ext.release_store_password
                keyAlias project.ext.release_key_alias
                keyPassword project.ext.release_key_password
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (project.hasProperty("release_store_file")) {
                signingConfig signingConfigs.release
            }
        }
    }
    productFlavors {
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    if (project.hasProperty("keys_json_file")) {
        apply plugin: 'com.github.triplet.play'

        serviceAccountCredentials = file(project.ext.keys_json_file)

        play {
            // Deactivate lower conflicting version APKs
            resolutionStrategy = "ignore"
            switch (System.env.TIER) {
                case 'beta':
                    track = 'beta'
                    break

                case 'stable':
                    track = 'production'
                    break

                default:
                    track = 'alpha'
            }
            println "TIER set to $track"
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
    maven {
        url 'https://maven.fabric.io/public'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    api(name: 'keyman-engine', ext: 'aar')
    implementation "com.google.firebase:firebase-core:16.0.5"
    implementation "com.google.firebase:firebase-crash:16.2.1"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.6@aar') {
        transitive = true
    }
}

/*
def void dumpProperties(it){
    //println "Examining $it.name:"
    //println "Meta:"
    //println it.metaClass.metaMethods*.name.sort().unique()
    //println "Methods:"
    //println it.metaClass.methods*.name.sort().unique()
    //println "Depends On:"
    //println it.dependsOn.collect({it*.getName()})
    println "Properties:"
    println it.properties.entrySet()*.toString()
            .sort().toString().replaceAll(", ","\n")
}
*/

